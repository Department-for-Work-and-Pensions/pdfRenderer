buildscript {
    ext {
        springBootVersion = '1.4.1.RELEASE'
    }
    repositories {
        jcenter()
        mavenCentral()
        maven {
            url "${artifactory_url}repo"
            credentials {
                username = "${artifactory_user}"
                password = "${artifactory_password}"
            }
        }
        maven {
            url 'http://jaspersoft.artifactoryonline.com/jaspersoft/third-party-ce-artifacts/'
        }
        maven {
            url 'http://jaspersoft.artifactoryonline.com/jaspersoft/jr-ce-releases'
        }
        maven {
            url 'http://jasperreports.sourceforge.net/maven2'
        }
    }
    dependencies {
        classpath(group: 'org.jfrog.buildinfo', name: 'build-info-extractor-gradle', version: '4.4.9')
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath('com.github.gmazelier:jasperreports-gradle-plugin:0.3.2'){
            exclude group: 'net.sf.jasperreports', module: 'jasperreports'
        }
        classpath("net.sf.jasperreports:jasperreports:6.3.1")
        classpath("com.itextpdf:itextpdf:5.5.10")
        classpath("net.sf.jasperreports:jasperreports-functions:6.3.1")
        classpath 'com.kenshoo:gradle-fpm:0.5'
    }
}

String candidate = "$System.env.BRANCH_NAME".contentEquals('feature/future') ? 'RELEASE' : 'SNAPSHOT'

allprojects {
    apply plugin: 'com.jfrog.artifactory'
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'maven-publish'
    apply plugin: 'spring-boot'
    apply plugin: 'com.github.gmazelier.jasperreports'
    apply plugin: 'fpm-packaging'
    version = "4.05-${candidate}"
    archivesBaseName = 'p1'
    group = 'gov.dwp.carers'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

String repo = version.endsWith('SNAPSHOT') ? 'libs-snapshot-local' : 'libs-release-local'

artifactory {
    contextUrl = "${artifactory_url}"   //The base Artifactory URL if not overridden by the publisher/resolver
    publish {
        mavenLocal()
        repository {
            repoKey = repo
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true

        }
        defaults {
            // Reference to Gradle publications defined in the build script.
            // This is how we tell the Artifactory Plugin which artifacts should be
            // published to Artifactory.
            publications('mavenJava', 'distribution')
            publishArtifacts = true
            publishPom = true // Publish generated POM files to Artifactory (true by default)
        }
    }
    resolve {
        repository {
            repoKey = 'repo'
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true
        }
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'http://jaspersoft.artifactoryonline.com/jaspersoft/third-party-ce-artifacts/'
    }
    maven {
        url 'http://jaspersoft.artifactoryonline.com/jaspersoft/jr-ce-releases/'
    }
    maven {
        url 'http://jasperreports.sourceforge.net/maven2'
    }
}

configurations {
    compile.exclude module: "spring-boot-starter-tomcat"
}

configurations.all {
    // check for updates every build
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {
    compile("net.sf.jasperreports:jasperreports:6.3.1")
    compile("com.itextpdf:itextpdf:5.5.10")
    compile("com.adobe.xmp:xmpcore:6.1.10")
    compile("gov.dwp.carers:fonts:1.0")
    compile("xalan:xalan:2.7.2")
    compile("gov.dwp.carers:xmlcommons:8.24-SNAPSHOT")
    compile("gov.dwp.carers:carerscommon:8.06-SNAPSHOT")
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-jetty")
    compile("org.springframework.boot:spring-boot-starter-data-rest")
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-websocket")
    compile("org.springframework.boot:spring-boot-devtools")
    compile("net.sf.jasperreports:jasperreports-functions:6.3.1")
    compile("org.springframework.boot:spring-boot-starter-actuator")
    testCompile (
            'org.springframework.boot:spring-boot-starter-test'
    )
    runtime("me.moocar:logback-gelf:0.3")
}

//Use this for publishing to local Maven repo
//gradle publishToMavenLocal
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact (sourcesJar) {
                classifier = 'sources'
            }

        }
        distribution(MavenPublication) {
            artifact ("${libsDir}/${archivesBaseName}-${version}-full.jar") {
                classifier = 'full'
            }
        }
    }
}

rpm {
    packageName = "${archivesBaseName}-${version}" // Optional, default is project.name
    dependencies = ['pdfreports'] //Optional, an array of package dependencies
    baseDir = "${project.buildDir}/build/libs" // Optional, base directory to package, default: project.buildDir
    prefix = "/data/carers/${archivesBaseName}/${archivesBaseName}-${version}" // Optional, a path to prefix files when building package, default: root (/)
    extraOptions = ['--vendor': 'DWP', '--maintainer': 'CADS Team'] // Optional, a map containing extra options
    extraFlags   = ['--rpm-auto-add-directories'] // Optional, a list of additional flags to fpm
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task createInfoFile << {
    def props = new Properties()
    props.setProperty("application.version", version)
    props.setProperty("application.name", rootProject.name)
    def writer = new FileWriter(file('./src/main/resources/config/application-info.properties'))
    try {
        props.store(writer, "application-info.properties Created for build version:" + version)
        writer.flush()
    } finally {
        writer.close()
    }
}

compileJava.dependsOn(createInfoFile)

task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}

test {
    testLogging.showStandardStreams = true
}

processResources {
    exclude('**/*.jrxml')
}

bootRun {
    addResources = true
}

jasperreports {
    srcDir = file("src/main/resources/config")
    tmpDir = file("${buildDir}/jasperreports")
    outDir = file("${buildDir}/resources/main/config")
    srcExt = '.jrxml'
    outExt = '.jasper'
    keepJava = false
    validateXml = true
    verbose = true
    useRelativeOutDir = true
    classpath = []
}

compileJava.dependsOn(compileAllReports)

def renameFile(oldName, newName) {
    new File(oldName).renameTo(new File(newName))
}

bootRepackage.doLast {
    renameFile("${libsDir}/${archivesBaseName}-${version}.jar", "${libsDir}/${archivesBaseName}-${version}-full.jar");
    renameFile("${libsDir}/${archivesBaseName}-${version}.jar.original", "${libsDir}/${archivesBaseName}-${version}.jar");
}
